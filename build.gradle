plugins {
  id 'java'
  id 'application'
  id 'org.flywaydb.flyway' version '4.0.1'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'maven-publish'

group 'uk.gov.bis.lite'
version '1.1'

mainClassName = "uk.gov.bis.lite.permissions.PermissionsApp"

repositories {
  mavenCentral()
  maven {
    name "snapshots"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
  }
  maven {
    name "releases"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
  }
}

dependencies {
  compile 'io.dropwizard:dropwizard-core:1.0.3'
  compile 'io.dropwizard:dropwizard-db:1.0.3'
  compile 'io.dropwizard:dropwizard-jdbi:1.0.3'
  compile 'io.dropwizard:dropwizard-auth:1.0.3'
  compile 'io.dropwizard:dropwizard-client:1.0.3'
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'org.quartz-scheduler:quartz:2.1.5'
  compile 'org.xerial:sqlite-jdbc:3.8.11.2'
  compile 'org.flywaydb:flyway-core:4.0.1'

  // Lite dependencies
  compile 'uk.gov.bis.lite:spire-client:1.2'
  compile 'uk.gov.bis.lite:lite-customer-service-api:1.1'
  compile 'uk.gov.bis.lite:jersey-correlation-id:1.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile 'io.dropwizard:dropwizard-testing:1.0.3'
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile('com.github.tomakehurst:wiremock:1.58') {
    exclude group: 'org.mortbay.jetty', module: 'servlet-api'
  }
  testCompile 'org.skyscreamer:jsonassert:1.3.0'

  testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.23') {
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
    exclude group: 'junit', module: 'junit'
  }
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/permissions/api/**/*'
}

task jarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/permissions/api/**/*'
}

publishing {
  publications {

    mavenJava(MavenPublication) {
      artifactId 'lite-permissions-service-api'
      artifact apiJar
      artifact jarWithSources {
        classifier "sources"
      }
    }
  }
}

publishing {
  repositories {
    maven {
      name "snapshots"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
    }
    maven {
      name "releases"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
    }
  }
}

flyway {
  url = 'jdbc:sqlite:localPermissions.db'
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'src/main/resources/permissions-config.yaml' }
