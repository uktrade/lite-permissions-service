import java.text.SimpleDateFormat

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'au.com.dius:pact-jvm-provider-gradle_2.11:3.3.7'
  }
}

plugins {
  id 'java'
  id 'application'
  id 'org.flywaydb.flyway' version '4.0.1'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "au.com.dius.pact" version "3.3.7"
}

apply plugin: 'maven-publish'

group 'uk.gov.bis.lite'

if (project.hasProperty('projVersion')) {
  project.version = project.projVersion
} else {
  SimpleDateFormat sdf = new SimpleDateFormat("YYYYMMdd.HHmmss")
  sdf.setTimeZone(new SimpleTimeZone(SimpleTimeZone.UTC_TIME, "UTC"))
  project.version = sdf.format(new Date()) + "-SNAPSHOT"
}

mainClassName = "uk.gov.bis.lite.permissions.PermissionsApp"

ext.dropwizardVersion = '1.2.0'

repositories {
  mavenCentral()
  maven {
    name "snapshots"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
  }
  maven {
    name "releases"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
  }
}

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-db:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'org.quartz-scheduler:quartz:2.1.5'
  compile 'org.xerial:sqlite-jdbc:3.8.11.2'
  compile 'org.flywaydb:flyway-core:4.0.1'
  compile 'net.logstash.logback:logstash-logback-encoder:4.10'
  compile 'ch.qos.logback.contrib:logback-jackson:0.1.5'

  // Lite dependencies
  compile 'uk.gov.bis.lite:json-console-appender:1.1'
  compile 'uk.gov.bis.lite:spire-client:1.6'
  compile 'uk.gov.bis.lite:jwt:1.0'
  compile 'uk.gov.bis.lite:lite-customer-service-api:1.1'
  compile 'uk.gov.bis.lite:jersey-correlation-id:1.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile('com.github.tomakehurst:wiremock:2.7.1') {
    exclude group: 'org.mortbay.jetty', module: 'servlet-api'
  }
  testCompile 'org.skyscreamer:jsonassert:1.3.0'

  testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.23') {
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
    exclude group: 'junit', module: 'junit'
  }
  testCompile 'org.awaitility:awaitility-groovy:3.0.0'
  // Pact
  testCompile 'au.com.dius:pact-jvm-consumer-junit_2.11:3.3.7'
  testCompile 'au.com.dius:pact-jvm-provider-junit_2.11:3.3.7'
  testCompile 'org.apache.httpcomponents:fluent-hc:4.5'
}

pact {
  publish {
    pactDirectory = 'target/pacts'
    pactBrokerUrl = 'http://pact-broker.mgmt.licensing.service.trade.gov.uk.test/'
    version = '1.0.3'
  }
}

flyway {
  url = 'jdbc:sqlite:localPermissions.db'
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'src/main/resources/config.yaml' }


publishing {
  repositories {
    maven {
      name "snapshots"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
    }
    maven {
      name "releases"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
    }
    maven {
      name "lite-builds"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/lite-builds/"
    }
  }
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/permissions/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/permissions/api/**/*'
}

publishing {
  publications {
    api(MavenPublication) {
      artifactId 'lite-permissions-service-api'
      version '1.3'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    service(MavenPublication) {
      artifactId 'permissions-service'
      from components.shadow
    }
  }
}